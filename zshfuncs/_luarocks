#compdef luarocks

# Copyright (C) 2018 Tomoyuki Fujimori <moyu@dromozoa.com>
#
# This file is part of dromozoa-dotfiles.
#
# dromozoa-dotfiles is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# dromozoa-dotfiles is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with dromozoa-dotfiles.  If not, see <http://www.gnu.org/licenses/>.

_luarocks() {
  local curcontext="$curcontext"
  local context state state_descr line
  typeset -A opt_args

  local -a options
  _dromozoa_luarocks_options

  _arguments -C \
    $options \
    ": :_luarocks_arguments __MARKER__ command" \
    "*:: :->argument"

  case x$state in
    xargument)
      _luarocks_command "$words[1]";;
  esac
}

_luarocks_command() {
  local curcontext="$curcontext"
  local context state state_descr line
  typeset -A opt_args

  local -a options
  _dromozoa_luarocks_options "$1"

  local -a arguments
  _dromozoa_luarocks_arguments "$1"

  _arguments -C $options $arguments
}

_luarocks_arguments() {
  local i j k
  local -a arguments

  for i in "$@"
  do
    shift
    case x$i in
      x__MARKER__) break;;
    esac
  done

  for i in "$@"
  do
    case x$i in
      xcommand)
        _dromozoa_luarocks_commands;;
      xname)
        luarocks list --porcelain 2>/dev/null | while read j k
        do
          arguments=($arguments "$j")
        done;;
      xrock)
        arguments=($arguments *.rock);;
      xrockspec)
        arguments=($arguments *.rockspec);;
    esac
  done

  _describe arguments arguments
}
