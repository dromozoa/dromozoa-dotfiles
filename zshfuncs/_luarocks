#compdef luarocks

# Copyright (C) 2018 Tomoyuki Fujimori <moyu@dromozoa.com>
#
# This file is part of dromozoa-dotfiles.
#
# dromozoa-dotfiles is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# dromozoa-dotfiles is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with dromozoa-dotfiles.  If not, see <http://www.gnu.org/licenses/>.

_luarocks() {
  local curcontext="$curcontext"
  local context state state_descr line
  typeset -A opt_args

  local -a options
  __dromozoa_luarocks_general_options

  _arguments -C \
    $options \
    ": :->command" \
    "*:: :->argument"

  local result
  case x$state in
    xcommand) __dromozoa_luarocks_command;;
    xargument) _call_function result "_luarocks_$words[1]";;
  esac
}

_luarocks_help() {
  local curcontext="$curcontext"
  local context state state_descr line
  typeset -A opt_args

  local -a options
  __dromozoa_luarocks_general_options

  _arguments -C \
    $options \
    ": :->command"

  case x$state in
    xcommand) __dromozoa_luarocks_command;;
  esac
}

_luarocks_list() {
  local curcontext="$curcontext"
  local context state state_descr line
  typeset -A opt_args

  local -a options
  __dromozoa_luarocks_general_options
  __dromozoa_luarocks_list_options

  _arguments -C \
    $options
}

_luarocks_remove() {
  local curcontext="$curcontext"
  local context state state_descr line
  typeset -A opt_args

  local -a options
  __dromozoa_luarocks_general_options
  __dromozoa_luarocks_remove_options
  local -a modes
  modes=(all one order none)

  _arguments -C \
    $options \
    ": :->name"

  local result
  case x$state in
    xmode) __dromozoa_luarocks_mode;;
    xname) __dromozoa_luarocks_name;;
  esac
}

__dromozoa_luarocks_command() {
  local -a commands
  __dromozoa_luarocks_commands
  _describe commands commands
}

__dromozoa_luarocks_name() {
  local -a names
  luarocks list --porcelain | while read name version _
  do
    names=($names "${name}:$version")
  done
  _describe names names
}

__dromozoa_luarocks_mode() {
  local -a modes
  modes=(all one order none)
  _describe modes modes
}
