#compdef dromozoa-boot

# Copyright (C) 2018 Tomoyuki Fujimori <moyu@dromozoa.com>
#
# This file is part of dromozoa-dotfiles.
#
# dromozoa-dotfiles is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# dromozoa-dotfiles is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with dromozoa-dotfiles.  If not, see <http://www.gnu.org/licenses/>.

_dromozoa_boot() {
  local curcontext="$curcontext"
  local context state state_descr line
  typeset -A opt_args

  _arguments -C \
    '(- *)'{-h,--help} \
    '(- *)'{-l,--list} \
    '(-k --insecure)'{-k,--insecure} \
    '(-p --prefix)'{-p,--prefix=}':prefix:_files' \
    '*:: :_dromozoa_boot_packages __MARKER__ $words'
}

_dromozoa_boot_packages() {
  local i j dummy skip
  local -a packages

  for i in "$@"
  do
    shift
    case x$i in
      x__MARKER__) break;;
    esac
  done

  (./dromozoa-boot --list 2>/dev/null || :) | while read i dummy
  do
    skip=
    for j in "$@"
    do
      if test "x$i" = "x$j"
      then
        skip=true
        break
      fi
    done
    case x$skip in
      x) packages=($packages "$i");;
    esac
  done

  _describe packages packages
}
